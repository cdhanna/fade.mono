#constant EASING_LINEAR 0
#constant EASING_BOUNCE 2

goto routines_exit

load_audio:
    global EXPLODE_SOUND_COUNT=2
    global dim EXPLODE_SOUNDS(2)

    EXPLODE_SOUNDS(0) = 0
    EXPLODE_SOUNDS(1) = 1

    load sfx clip EXPLODE_SOUNDS(0), "Fish/Audio/bubble-pop-2"
    load sfx clip EXPLODE_SOUNDS(1), "Fish/Audio/bubble-pop-4"
    
    load sfx clip 100, "Fish/Audio/underwater-ambiencewav-14428"


    global SFX_CLIP_SWAP    
    load sfx clip reserve sfx clip id(SFX_CLIP_SWAP), "Fish/Audio/river-and-stones"
    global SFX_SWAP
    sfx reserve sfx id(SFX_SWAP), 100
    set sfx loop SFX_SWAP, 1
    set sfx volume SFX_SWAP 0
    play sfx SFX_SWAP


    global SFX_CLIP_DROP
    load sfx clip reserve sfx clip id(SFX_CLIP_DROP), "Fish/Audio/rocks-falling"
    global SFX_DROP
    sfx reserve sfx id(SFX_DROP), SFX_CLIP_DROP
    set sfx loop SFX_DROP, 1
    set sfx volume SFX_DROP, 0
    play sfx SFX_DROP

    global SFX_TWEEN_DROP
    reserve tween id(SFX_TWEEN_DROP)




    sfx 100,100
    `play sfx 100
    set sfx loop 100, 1

return

load_fonts:

    global SCORE_FONT = 1

    font SCORE_FONT, "Fish/Fonts/ZeroCool"


    global SCORE_TEXT_COUNT = 100
    global SCORE_TEXT_NUM = 0
    global dim SCORE_TEXTS(SCORE_TEXT_COUNT) as floatingText

    for i = 0 to SCORE_TEXT_COUNT - 1
        SCORE_TEXTS(i) = {
            active = 0
            textId = i + 1 `TODO: add reserve, 
            floatTweenIdX = reserve tween id(n)
            floatTweenIdY = reserve tween id(n)
            fadeTweenId = reserve tween id(n)
            fadeInTweenId = reserve tween id(n)
        }
        text SCORE_TEXTS(i).textId, 0, 0, SCORE_FONT, ""
        `hide text SCORE_TEXTS(i).textId
    next

return

load_textures:
    global TEX_PIXEL = 0 `magic texture that always exists
    texture free texture id(TEX_PUFFER), "Fish/Textures/Pufferfish"
    texture free texture id(TEX_CATFISH), "Fish/Textures/Catfish"
    texture free texture id(TEX_GOLDFISH), "Fish/Textures/Goldfish"
    texture free texture id(TEX_ANGELFISH), "Fish/Textures/Angelfish"
    texture free texture id(TEX_CRAB), "Fish/Textures/Crab - Dungeness"
    texture free texture id(TEX_BASS), "Fish/Textures/Bass"
    texture free texture id(TEX_WORM), "Fish/Textures/Worm"

    texture reserve texture id(TEX_NOISE), "Fish/Textures/Turbulence_13-256x256"

    global dim FISH_COLORS(7)
    FISH_COLORS(getFishColorIndex(TEX_PUFFER)) = rgb(250, 190, 54)
    FISH_COLORS(getFishColorIndex(TEX_CATFISH)) = rgb(160, 150, 121)
    FISH_COLORS(getFishColorIndex(TEX_GOLDFISH)) = rgb(255, 174, 48)
    FISH_COLORS(getFishColorIndex(TEX_ANGELFISH)) = rgb(255, 211, 52)
    FISH_COLORS(getFishColorIndex(TEX_CRAB)) = rgb(223, 55, 55)
    FISH_COLORS(getFishColorIndex(TEX_BASS)) = rgb(154, 181, 89)
    FISH_COLORS(getFishColorIndex(TEX_WORM)) = rgb(255, 144, 144)

    texture free texture id(TEX_SMOKE_1), "Fish/Textures/Free Smoke Fx  Pixel 04"
    set texture frame grid TEX_SMOKE_1, 10 8 `rows, columns

    texture free texture id(TEX_SMOKE_2), "Fish/Textures/Free Smoke Fx  Pixel 06"
    set texture frame grid TEX_SMOKE_2, 23 12 `rows, columns

    global SMOKE_ANIM_COUNT = 5
    global dim SMOKE_ANIMS(SMOKE_ANIM_COUNT) as smokeAnimInfo
    SMOKE_ANIMS(0) = {
        texId = TEX_SMOKE_1,
        startFrame = 8 * 2
        endFrame = 8 * 3
    }
    SMOKE_ANIMS(1) = {
        texId = TEX_SMOKE_1,
        startFrame = 8 * 7
        endFrame = 8 * 8
    }
    SMOKE_ANIMS(2) = {
        texId = TEX_SMOKE_2,
        startFrame = 12 * 2
        endFrame = 12 * 3
    }
    SMOKE_ANIMS(3) = {
        texId = TEX_SMOKE_2,
        startFrame = 12 * 4
        endFrame = 12 * 5
    }
    SMOKE_ANIMS(4) = {
        texId = TEX_SMOKE_2,
        startFrame = 12 * 6
        endFrame = 12 * 7
    }

    global SMOKE_COLOR_COUNT = 5
    global dim SMOKE_COLORS(SMOKE_COLOR_COUNT)
    SMOKE_COLORS(0) = rgb(143, 199, 255)
    SMOKE_COLORS(1) = rgb(120, 172, 224)
    SMOKE_COLORS(2) = rgb(143, 199, 255)
    SMOKE_COLORS(3) = rgb(141, 198, 255)
    SMOKE_COLORS(4) = rgb(121, 171, 222)


return

setup_grid:

    global CELL_SIZE = 28
    global CELL_ART_SIZE = 16
    global GRID_POS as vec = {
        x = 32 + 16, 
        y = 64
    }


    global CELL_COLUMN_COUNT = 9
    global CELL_ROW_COUNT = 9
    global CELL_COUNT as int = CELL_COLUMN_COUNT * CELL_ROW_COUNT
    global dim CELLS(CELL_COUNT) as cell

    global DEBUG_COLOR_COUNT = 1000
    global dim DEBUG_COLORS(DEBUG_COLOR_COUNT)
    for i = 1 to DEBUG_COLOR_COUNT
        DEBUG_COLORS(i-1) = rgb(rnd(255), rnd(255), rnd(255))
    next

    global REQUEST_COMBO_CHECK = 0
    global REQUEST_ABILITY_SELECTION = 0

    cellId = 0
    for y = 1 to CELL_ROW_COUNT
        for x = 1 to CELL_COLUMN_COUNT
            CELLS(cellId) = {
                coord = {
                    x = x - 1, 
                    y = y - 1    
                }
                value = getRandomValue()
            }

            CELLS(cellId).value = getImpossibleRandomValueForCell(cellId)

            reserve sprite id(CELLS(cellId).spriteId)
            reserve sprite id(CELLS(cellId).fxSpriteId)
            reserve tween id(CELLS(cellId).comboDownShiftTweenId)
            reserve tween id(CELLS(cellId).comboAlphaTweenId)
            reserve tween id(CELLS(cellId).comboAnimTweenId)
            reserve tween id(CELLS(cellId).scaleWaveTweenId)
            create basic tween CELLS(cellId).scaleWaveTweenId, 1, 1, 100, 0

            reserve sfx id(CELLS(cellid).explodeSfxId)

            c as cell = CELLS(cellId)
            sprite c.spriteId, getCellX(c.coord.x), getCellY(c.coord.y), c.value
            set sprite stage c.spriteId, 2

            sprite c.fxSpriteId, getCellX(c.coord.y), getCellY(c.coord.y), TEX_SMOKE_1
            hide sprite c.fxSpriteId

            size sprite c.spriteId, CELL_ART_SIZE, CELL_ART_SIZE
            inc cellId


        next
    next
return

setup_score:

    global SCORE_POS as vec = {
        x = 32,
        y = 24
    }

    global SCORE_SIZE as vec = {
        x = CELL_COLUMN_COUNT * CELL_SIZE, 
        y = 4
    }
    global SCORE_BAR as progressBar = createProgressBar(SCORE_POS, SCORE_SIZE)
    SCORE_BAR.startVal = 0
    SCORE_BAR.endVal = 200
    
    global dim PROGRESS_BARS(1) as progressBar
    global PROGRESS_BAR_COUNT = 1
    PROGRESS_BARS(0) = SCORE_BAR

    ` global SCORE_SLIDE_BACKDROP_SPRITE
    ` global SCORE_SLIDE_FRONT_SPRITE
    ` global SCORE_SLIDE_TWEEN_ID

    ` reserve sprite id(SCORE_SLIDE_BACKDROP_SPRITE)
    ` reserve sprite id(SCORE_SLIDE_FRONT_SPRITE)
    ` reserve tween id(SCORE_SLIDE_TWEEN_ID)

    global CURRENT_SCORE = 0

    ` sprite SCORE_SLIDE_BACKDROP_SPRITE, SCORE_POS.x, SCORE_POS.y, TEX_PIXEL
    ` size sprite SCORE_SLIDE_BACKDROP_SPRITE, CELL_COLUMN_COUNT * CELL_SIZE, 4
    ` set sprite offset SCORE_SLIDE_BACKDROP_SPRITE, 0, 0
    ` color sprite SCORE_SLIDE_BACKDROP_SPRITE, rgb(32, 64, 128, 128)

    ` sprite SCORE_SLIDE_FRONT_SPRITE, SCORE_POS.x + 1, SCORE_POS.y + 1, TEX_PIXEL
    ` size sprite SCORE_SLIDE_FRONT_SPRITE, 100, 2
    ` set sprite offset SCORE_SLIDE_FRONT_SPRITE, 0, 0
    ` color sprite SCORE_SLIDE_FRONT_SPRITE, rgb(64, 128, 255, 200)

return




setup_selection:

    global COLLECTED_INPUT_MASK as long = 0
    global SELECTION_POS as vec = {
        x = CELL_COLUMN_COUNT / 2, y = CELL_ROW_COUNT / 2
    }

    global QUEUED_MOVE_MAX = 100
    global dim QUEUED_MOVES(QUEUED_MOVE_MAX) as move
    global QUEUED_MOVE_NUM
    global QUEUED_MOVE_TAIL

    global SELECTION_SPRITE
    reserve sprite id(SELECTION_SPRITE)

    gosub update_selection_position
    size sprite SELECTION_SPRITE, CELL_SIZE, CELL_SIZE
    order sprite SELECTION_SPRITE, -1
    color sprite SELECTION_SPRITE, rgb(60, 90, 170, 170)
return


setup_swap_tweens:

    global TWEEN_SWAP_AX
    global TWEEN_SWAP_BX
    global TWEEN_SWAP_AY
    global TWEEN_SWAP_BY
    
    reserve tween id(TWEEN_SWAP_AX)
    reserve tween id(TWEEN_SWAP_AY)
    reserve tween id(TWEEN_SWAP_BX)
    reserve tween id(TWEEN_SWAP_BY)

    global SHAKE_TWEEN_ID
    reserve tween id (SHAKE_TWEEN_ID)

    global SELECTION_TWEEN_PUNCH_ID
    reserve tween id (SELECTION_TWEEN_PUNCH_ID)

    global SWAP_TWEEN_SFX
    reserve tween id (SWAP_TWEEN_SFX)

return


update_selection_position:
    position sprite SELECTION_SPRITE, getCellX(SELECTION_POS.x), getCellY(SELECTION_POS.y)
return


time# = 0
update_frame:
    gosub frame_texts
    gosub frame_scale_wave
    gosub frame_score


`     for i = 0 to CELL_COUNT - 1
`         c = CELLS(i)
`         spriteId = c.spriteId


`         xd = SELECTION_POS.x - c.coord.x
`         yd = SELECTION_POS.y - c.coord.y
       
`         z# = 12
`         yaw# = deg(atan2(-xd, z#))
`         pitch# = deg(atan2(yd, sqrt(xd * xd + z#*z#)))
` `float yaw = MathF.Atan2(x, z); // rotation around Y axis
` `float pitch = MathF.Atan2(-y, MathF.Sqrt(x * x + z * z)); // rotation around X axis

`         set sprite all texcoord1 spriteId, pitch#, yaw#, 0, 0

`         if i mod 14 = 1 or i mod 17 = 2
`             set sprite texture spriteId, 0
`             set sprite all texcoord1 spriteId, pitch# + time# * 2, yaw#, 0, 0

`         endif

`     next
`     time# += 2
    
return

frame_score:

    if CURRENT_SCORE >= SCORE_BAR.endVal
        `CURRENT_SCORE = 0
        SCORE_BAR.startVal = SCORE_BAR.endVal
        SCORE_BAR.endVal = SCORE_BAR.endVal * 2
        print "start", SCORE_BAR.startVal, SCORE_BAR.endVal
        PROGRESS_BARS(0) = SCORE_BAR
        create basic tween SCORE_BAR.progressTweenId, tweenVal(SCORE_BAR.progressTweenId), 0, 100, 0
    endif

    for i = 0 to PROGRESS_BAR_COUNT - 1
        bar = PROGRESS_BARS(i)
        scoreWidth# = (bar.size.x - 2.0) * tweenVal(bar.progressTweenId)
        size sprite bar.foregroundSpriteId, scoreWidth#, 2
        span = bar.endVal - bar.startVal
        y$ = str$(span * tweenVal(bar.progressTweenId)) `+ "/" + str$(span)
        z$ = "/"
        x$ = str$(span)

        concat$ = y$ + z$ + x$
        set text bar.textNumId, str$(span * tweenVal(bar.progressTweenId)) + "/" + str$(span)
    next



return

frame_texts:
    for i = 0 to SCORE_TEXT_COUNT - 1
        scoreTxt = SCORE_TEXTS(i)

        if scoreTxt.active = 0
            `hide text scoreTxt.textId
        else 
            `show text scoreTxt.textId
        endif
        
        set text position scoreTxt.textId, tweenVal(scoreTxt.floatTweenIdX), tweenVal(scoreTxt.floatTweenIdY)

        if is tween done(scoreTxt.fadeInTweenId) > 0
            set text alpha scoreTxt.textId, tweenVal(scoreTxt.fadeTweenId)
        else 
            set text alpha scoreTxt.textId, tweenVal(scoreTxt.fadeInTweenId)
        endif
        ` TODO: add 'set text alpha' command
    next
return

frame_scale_wave:

    for i = 0 to CELL_COUNT - 1
        c = CELLS(i)
        scale = tweenVal(c.scaleWaveTweenId) * CELL_ART_SIZE
        size sprite c.spriteId, scale, scale
    next

return

handle_combos:

    remstart
        need to scan through the rows and find groups of similar valued cells 
        once a group of big-enough size is found, it blows up 
        all blow-ups happen "at once" (animated randomly) 
        then all cells need to "fall" to fill in gaps
        then new cells need to spawn in from the top and "fall" into place
    remend

    if REQUEST_COMBO_CHECK = 0 then return

    ` clear the combo mask... 
    for i = 1 to CELL_COUNT
        c = CELLS(i-1)
        c.comboFillMask = 0
        c.comboFillId = 0
        c.comboBlownUp = 0
        c.comboDownShift = 0
    
        CELLS(i-1) = c
    next

    groupId = 0
    for i = 1 to CELL_COUNT
        c = CELLS(i-1)
        
        if c.comboFillMask > 0 then skip

        ` flood fill from this cell if it has not already been combo checked
    
        CELLS(i-1).comboFillId = groupId
        findCombos(i - 1, groupId)
        groupId += 1
    next

    ` DEBUG: just color the groups for debug visuals
    for i = 1 to CELL_COUNT
        `color sprite CELLS(i-1).spriteId, DEBUG_COLORS(CELLS(i-1).comboFillId)
    next

    ` animate explosions of all groups
    cellId = 0

    totalComboCount = 0
    explodePitch# = (rnd(4)*.02)
    explodeIndex = 0
    for g = 0 to groupId - 1

        ` need to check if this group is big enough
        comboCount = 0
        for i = 1 to CELL_COUNT
            cell = CELLS(i-1)

            if comboCount > 2 then skip 
            if cell.comboFillId <> g then skip 
            comboCount += 1
        next

        if comboCount < 3 then skip `if this group is not big enough, then we can skip rendering the explosion

        totalComboCount += 1
        scoreAdd = 0

        ` animate the exit of the group
        randomG = (totalComboCount-1)
        
        for i = 1 to CELL_COUNT
            cell = CELLS(i-1)
            if cell.comboFillId <> g then skip 

            CELLS(i-1).comboBlownUp = 1
            cellId = i-1

            smokeIndex = rnd(SMOKE_ANIM_COUNT)
            smoke = SMOKE_ANIMS(smokeIndex)

            explodeIndex += 1
            delay = explodeIndex * 50`rnd(3)*75+25 `TODO: somehow use the delay to "randomize" the animation

            ` kaboom sprite
            create basic tween cell.comboAnimTweenId, smoke.startFrame, smoke.endFrame, 400, delay
            set tween easing cell.comboAnimTweenId, EASING_LINEAR
            show sprite cell.fxSpriteId
            rotate sprite cell.fxSpriteId, rnd(8) * .25
            color sprite cell.fxSpriteId, SMOKE_COLORS(rnd(SMOKE_COLOR_COUNT))
            sprite cell.fxSpriteId, getCellX(cell.coord.x), getCellY(cell.coord.y), smoke.texId

            ` sfx
            sfx cell.explodeSfxId, rnd(EXPLODE_SOUND_COUNT)
            delay play sfx cell.explodeSfxId, delay
            set sfx pitch cell.explodeSfxId, explodePitch#
            explodePitch# -= .02


            ` animate fade out
            create basic tween cell.comboDownShiftTweenId, 255, 0, 250, delay

            ` animate screen shake
            create basic tween SHAKE_TWEEN_ID, 0, 15, 350, 0
            set tween type SHAKE_TWEEN_ID, 1
            set screen shake bounce .5

            ` spawn score text
            txtPos as vec = {
                x = getCellX(cell.coord.x),
                y = getCellY(cell.coord.y)
            }
            scoreAdd += 10
            txtId = spawnScoreText(txtPos, "+" + str$(scoreAdd), delay)
            color text txtId, FISH_COLORS(getFishColorIndex(cell.value))

            ` create basic tween SCORE_BAR.progressTweenId, getScoreRatio(0), getScoreRatio(scoreAdd), 100, 50
            CURRENT_SCORE += scoreAdd
            setProgressTween(SCORE_BAR, CURRENT_SCORE)

        next

    next

    ` exit early if no combos
    if totalComboCount = 0
        REQUEST_COMBO_CHECK = 0
        return
    endif

    ` wait for all exit animations to finish
    repeat 
        anyStillPlaying = 0
        if is tween done(SHAKE_TWEEN_ID) < 1
            anyStillPlaying = 1
            set screen shake amount tweenVal(SHAKE_TWEEN_ID)
            else 
            set screen shake amount 0

        endif
        for i = 1 to CELL_COUNT
            cell = CELLS(i-1)
            if cell.comboBlownUp <> 1 then skip 

            if is tween done(cell.comboDownShiftTweenId) < 1 OR is tween done(cell.comboAnimTweenId) < 1
                anyStillPlaying = 1
            endif

            set sprite frame cell.fxSpriteId, tweenVal(cell.comboAnimTweenId)
            if tweenRatio(cell.comboAnimTweenId) > 0 AND is tween done(cell.comboAnimTweenId) < 1
                show sprite cell.fxSpriteId
            else
                hide sprite cell.fxSpriteId
            endif


            set sprite alpha cell.spriteId, tweenVal(cell.comboDownShiftTweenId)
            `position sprite cell.spriteId, sprite x(cell.spriteId), tweenVal(cell.comboDownShiftTweenId)
        next

        gosub update_frame
        sync
    until anyStillPlaying = 0

    ` for all cells that are not blown up, need to find out where they should live... 
    startIndexOfLastRow = CELL_COUNT - CELL_COLUMN_COUNT
    for x = 1 to CELL_COLUMN_COUNT

        ` start at the bottom and work up.
        index = startIndexOfLastRow + (x-1)

        ` work up the column looking for empty spots
        emptyCount = 0
        for y = 1 to CELL_ROW_COUNT

            cell = CELLS(index)
            if cell.comboBlownUp
                ` keep track that this cell is "gone" in the column
                emptyCount += 1
            else
                ` save that the cell needs to move so many units DOWN 
                CELLS(index).comboDownShift = emptyCount
            endif

            ` move the index UP the grid by 1 row
            index -= CELL_COLUMN_COUNT 
        next
    next

    ` and now for all cells, need to tween them to the correct position 
    for i = 1 to CELL_COUNT
        cell = CELLS(i-1)

        ` hide the fxSprite if it was used
        hide sprite cell.fxSpriteId

        create basic tween cell.comboDownShiftTweenId, getCellY(cell.coord.y), getCellY(cell.coord.y + cell.comboDownShift), 500, 0
        set tween easing cell.comboDownShiftTweenId, EASING_BOUNCE
    next

    while is tween done(CELLS(0).comboDownShiftTweenId) < 1
        for i = 1 to CELL_COUNT
            cell = CELLS(i-1)

            position sprite cell.spriteId, sprite x(cell.spriteId), tweenVal(cell.comboDownShiftTweenId)
        next

        gosub update_frame
        sync
    endwhile

    ` now all the cells have animated to the right spot; but we need to do 2 things...
    `  1. ACTUALLY move the shizam to the shoozum 
    `  2. all the blown up tiles need to be created and dropped in from the top 

    ` step 1; swap all the cells that dropped
    for x = 1 to CELL_COLUMN_COUNT

        ` start at the bottom and work up.
        index = startIndexOfLastRow + (x-1)
        for y = 1 to CELL_ROW_COUNT
            cell = CELLS(index)

            updateCell(cell)
            set sprite alpha cell.spriteId, 255

            droppedToIndex = index + (cell.comboDownShift*CELL_COLUMN_COUNT)

            tempValue = cell.value
            CELLS(index).value = CELLS(droppedToIndex).value
            CELLS(droppedToIndex).value = tempValue

            tempValue = cell.comboBlownUp
            CELLS(index).comboBlownUp = CELLS(droppedToIndex).comboBlownUp
            CELLS(droppedToIndex).comboBlownUp = tempValue

            ` this brings the correct value from the upper cell to the lower cell
            updateCell(CELLS(droppedToIndex))
            updateCell(CELLS(index))

            index -= CELL_COLUMN_COUNT

        next
    next

    set sfx volume SFX_DROP, 1

    ` step 2; spawn in new cells and animate them falling in  
    for i = 1 to CELL_COUNT
        cell = CELLS(i - 1)
        y = getCellY(cell.coord.y)
        targetY = y
        startAlpha = 255
        targetAlpha = 255
        if cell.comboBlownUp > 0
            CELLS(i-1).value = getImpossibleRandomValueForCell(i - 1)
            set sprite texture cell.spriteId, CELLS(i-1).value
            targetY = y - CELL_SIZE * 2
            startAlpha = 0
            `color sprite cell.spriteId, rgb(0,0,0)
        endif

        delay = cell.coord.x * 20 + (CELL_ROW_COUNT - cell.coord.y)*20
        create basic tween cell.comboDownShiftTweenId, targetY, y, 300, delay
        set tween easing cell.comboDownShiftTweenId, EASING_BOUNCE
        create basic tween cell.comboAlphaTweenId, startAlpha, targetAlpha, 150, delay
    next


    ` wait for all exit animations to finish
    repeat 
        anyStillPlaying = 0
        for i = 1 to CELL_COUNT
            cell = CELLS(i-1)
            if cell.comboBlownUp <> 1 then skip 

            if is tween done(cell.comboDownShiftTweenId) < 1
                anyStillPlaying = 1
            endif

            

            position sprite cell.spriteId, sprite x(cell.spriteId), tweenVal(cell.comboDownShiftTweenId)
            set sprite alpha cell.spriteId, tweenVal(cell.comboAlphaTweenId)
        next

        gosub update_frame
        sync
    until anyStillPlaying = 0

    set sfx volume SFX_DROP, 0

return



end `TODO: this shouldn't need to exist because the `return` statement... without this, I see a "symbol already declared" 
routines_exit:
